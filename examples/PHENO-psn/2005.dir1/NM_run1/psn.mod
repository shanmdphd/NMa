$PROBLEM    PHENOBARBITAL IV P:ROOT F:BASE
$INPUT      ID TIME AMT BWT APGA DV MDV EVID
$DATA      PHENO
$SUBROUTINE ADVAN1 TRANS2
$PK
  TVCL = THETA(1)
  TVV  = THETA(2)
  CL   = TVCL * EXP(ETA(1))
  V    = TVV  * EXP(ETA(2))
  S1   = V
  K    = CL / V
  HALF = LOG(2) / K

$ERROR
  IPRE  = F
  W     = SQRT(THETA(3)**2 + THETA(4)**2 * IPRE**2)
  IRES  = DV - IPRE
  IWRE  = IRES / W
  Y     = IPRE + W * EPS(1)

$THETA  (0,0.01)
 (0,1)
 (0,1)
 (0,0.1)
$OMEGA  BLOCK(2)
 0.2
 0.1 0.2
$SIGMA  1  FIX
$ESTIMATION MAXEVAL=9999 PRINT=5 METHOD=COND INTER MSFO=THIS2005.MSF
            NOTBT NOOBT NOSBT NOABORT
$COVARIANCE UNCOND
$TABLE      ID TIME MDV IPRE IWRE CWRES FILE=sdtab2005 NOPRINT
            ONEHEADER
$TABLE      ID CL V K HALF ETA(1) ETA(2) FILE=patab2005 NOPRINT
            ONEHEADER NOAPPEND
$TABLE      ID BWT APGA FILE=cotab2005 NOPRINT ONEHEADER NOAPPEND
$TABLE      ID APGA FILE=catab2005 NOPRINT ONEHEADER NOAPPEND

