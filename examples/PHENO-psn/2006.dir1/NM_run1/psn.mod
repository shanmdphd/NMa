$PROBLEM    PHENOBARBITAL IV P:ROOT F:BASE
$INPUT      ID TIME AMT BWT APGA DV MDV EVID
$DATA      PHENO
$SUBROUTINE ADVAN1 TRANS2
$PK
  CL = THETA(1) * EXP(ETA(1))
  V  = THETA(2) * EXP(ETA(2))
  S1 = V

$ERROR
  IPRE = F
  W = SQRT(THETA(3)**2*IPRE**2 + THETA(4)**2)
  Y = IPRE + W*EPS(1)
  IRES = DV-IPRE
  IWRE = IRES/W

$THETA  (0,1) ; CL
 (0,1) ; V
 (0,.1) ; Prop.RE (sd)
 (0,1) ; Add.RE (sd)
$OMEGA  0.1
 0.1
$SIGMA  1  FIX
$ESTIMATION MAX=9999 PRINT=5 METHOD=COND INTER MSFO=THIS2006.MSF NOTBT
            NOOBT NOSBT NOABORT
$COVARIANCE UNCOND
$TABLE      ID TIME DV MDV EVID IPRE IWRE CWRES FILE=sdtab2006 NOPRINT
            ONEHEADER
$TABLE      CL V ETA(1) ETA(2) FILE=patab2006 NOPRINT ONEHEADER
            NOAPPEND
$TABLE      ID BWT APGA FILE=cotab2006 NOPRINT ONEHEADER NOAPPEND
$TABLE      ID APGA FILE=catab2006 NOPRINT ONEHEADER NOAPPEND

