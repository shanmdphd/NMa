$PROB THEOPHYLLINE ORAL P:ROOT F:BASE
$INPUT ID AMT TIME DV BWT
$DATA THEOPP
$SUBR ADVAN2 TRANS2
$PK
  TVCL  = THETA(1)
  TVV   = THETA(2)
  TVKA  = THETA(3)
  CL    = TVCL * EXP(ETA(1))
  V     = TVV  * EXP(ETA(2))
  KA    = TVKA * EXP(ETA(3))
  S2    = V
  K     = CL / V

$ERROR
  IPRED  = F
  W     = SQRT(THETA(4)**2 + THETA(5)**2 * IPRED**2)
  IRES  = DV - IPRED
  IWRES  = IRES / W
  Y     = IPRED + W * EPS(1)

$THETA
  (0, 0.1)
  (0, 1)
  (0, 10)
  0.1 FIX
  (0, 0.5)

$OMEGA BLOCK(3)
  0.2
  0.1  0.2
  0.1  0.1  0.2

$SIGMA 1 FIX
$EST MAXEVAL=9999 PRINT=5 METHOD=COND INTER 
     NOTBT NOOBT NOSBT NOABORT MSFO=THIS.MSF
$COV UNCOND PRINT=E
$TAB ID TIME MDV IPRED IWRES CWRES
     FILE=sdtabC07-4 NOPRINT ONEHEADER
$TAB ID CL V KA K ETA(1) ETA(2) ETA(3)
     FILE=patabC07-4 NOPRINT ONEHEADER NOAPPEND
$TAB ID BWT
     FILE=cotabC07-4 NOPRINT ONEHEADER NOAPPEND
$TAB ID
     FILE=catabC07-4 NOPRINT ONEHEADER NOAPPEND
