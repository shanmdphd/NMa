$PROB THEOPHYLLINE ORAL
$INPUT ID AMT=DROP TIME DV BWT
$DATA ../THEO
$PRED
  DOSE = 320
  V    = THETA(1) * EXP(ETA(1))
  K    = THETA(2) * EXP(ETA(2))
  KA   = THETA(3) * EXP(ETA(3))
  F    = DOSE / V * KA / (KA - K) * (EXP(-K*TIME) - EXP(-KA*TIME))
  IPRE = F
  Y    = F + EPS(1)

  IF (ICALL.EQ.3) THEN
    WRITE (53,*) OMEGA(BLOCK)
    WRITE (54,*) SIGMA(BLOCK)
  ENDIF

$THETA (0, 50, 500) (0, 1, 10) (0, 5, 50)
$OMEGA BLOCK(3)
  0.2
  0.1  0.2
  0.1  0.1  0.2
$SIGMA 0.1
$EST MAX=9999 METHOD=COND
$COV UNCOND
$TAB ID TIME IPRE CPRED CPREDI CWRES G11 G21 G31 H11
     FILE=sdtab NOPRINT ONEHEADER
$TAB ID ETA(1) ETA(2) ETA(3)
     FILE=patab NOPRINT ONEHEADER NOAPPEND FIRSTONLY
