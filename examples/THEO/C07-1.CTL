$PROB THEOPHYLLINE ORAL
$INPUT ID AMT=DROP TIME DV BWT
$DATA ../THEO
$PRED
  DOSE = 320
  V    = THETA(1) * EXP(ETA(1))
  K    = THETA(2) * EXP(ETA(2))
  KA   = THETA(3) * EXP(ETA(3))
  F    = DOSE / V * KA / (KA - K) * (EXP(-K*TIME) - EXP(-KA*TIME))
  IPRE = F
  Y    = F + F * EPS(1) + EPS(2)

  CL   = V * K

$THETA (0, 50, 500) (0, 1, 10) (0, 5, 50)
$OMEGA BLOCK(3)
  0.2
  0.1  0.2
  0.1  0.1  0.2
$SIGMA 0.1  0.1
$EST MAX=9999 PRINT=5 METHOD=COND INTER 
     NOTBT NOOBT NOSBT NOABORT MSFO=THIS.MSF
$COV UNCOND PRINT=ERS
$TAB ID TIME IPRE CWRES
     FILE=sdtab NOPRINT ONEHEADER
$TAB ID V K KA CL ETA(1) ETA(2) ETA(3)
     FILE=patab NOPRINT ONEHEADER NOAPPEND
$TAB ID BWT
     FILE=cotab NOPRINT ONEHEADER NOAPPEND
$TAB ID
     FILE=catab NOPRINT ONEHEADER NOAPPEND
