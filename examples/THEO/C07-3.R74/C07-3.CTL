$PROB THEOPHYLLINE ORAL P:ROOT F:BASE
$INPUT ID AMT TIME DV BWT
$DATA ../THEOPP
$SUBR ADVAN2 TRANS2
$PK
  TVCL  = THETA(1)
  TVV   = THETA(2)
  TVKA  = THETA(3)
  CL    = TVCL * EXP(ETA(1))
  V     = TVV  * EXP(ETA(2))
  KA    = TVKA * EXP(ETA(3))
  S2    = V
  K     = CL / V

$ERROR
  IPRE  = F
  W     = SQRT(THETA(4)**2 + THETA(5)**2 * IPRE**2)
  IRES  = DV - IPRE
  IWRE  = IRES / W
  Y     = IPRE + W * EPS(1)

$THETA
  (0, 0.1)
  (0, 1)
  (0, 10)
  (0, 1)
  (0, 0.5)

$OMEGA
  0.2
  0.2
  0.2

$SIGMA 1 FIX
$EST MAXEVAL=9999 PRINT=5 METHOD=COND INTER 
     NOTBT NOOBT NOSBT NOABORT MSFO=THIS.MSF
$COV UNCOND PRINT=E
$TAB ID TIME MDV IPRE IWRE CWRES
     FILE=sdtab NOPRINT ONEHEADER
$TAB ID CL V KA K ETA(1) ETA(2) ETA(3)
     FILE=patab NOPRINT ONEHEADER NOAPPEND
$TAB ID BWT
     FILE=cotab NOPRINT ONEHEADER NOAPPEND
$TAB ID
     FILE=catab NOPRINT ONEHEADER NOAPPEND
